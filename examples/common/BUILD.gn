# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("common") {
  defines = [ "VULKAN_HPP_NO_EXCEPTIONS" ]
  sources = [
    "demo.cc",
    "demo.h",
    "demo_harness.cc",
    "demo_harness.h",
    "vulkan_proc_addrs.h",
    "vulkan_proc_addrs.cc",
  ]
  deps = [
    "//lib/escher/escher",
    "//lib/ftl",
    "//third_party/shaderc/third_party/glslang",
    "//third_party/shaderc/third_party/glslang:SPIRV",
    "//third_party/shaderc/third_party/glslang:glslang-default-resource-limits",
  ]
  libs = [
    "dl",
    "pthread",
  ]
  include_dirs = [
    "//lib",
    "//lib/escher",
    "//third_party/glm",
  ]

  if (is_fuchsia) {
    deps += [
      "//application/lib/app:app",
      "//apps/modular/services/module:module_cpp_sources",
      "//magma:vulkan",
      "//lib/mtl",
      "//lib/escher/examples/common/services",
    ]
    sources += [
      "demo_harness_fuchsia.cc",
      "demo_harness_fuchsia.h",
    ]
  }

  if (is_linux) {
    configs += [ "//lib/escher:vulkan_linux" ]
    deps += [ "third_party:glfw" ]
    sources += [
      "demo_harness_linux.cc",
      "demo_harness_linux.h",
    ]
    libs += [
      "X11",
      "Xinerama",
      "Xcursor",
      "Xrandr",
    ]

    include_dirs += [ "third_party/glfw/include" ]
  }
}
